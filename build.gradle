apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'maven-publish'

// Artifact settings
group = 'be.lukin.poeditor'
version = '0.2.0'
archivesBaseName = 'poeditor-client'

// http://gradle.org/docs/current/userguide/application_plugin.html
mainClassName = "be.lukin.poeditor.JarMain"

def _name = 'POEditor API Client'
def _description = 'API Client for the POEditor API'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    manifest {
        attributes 'Main-Class': 'be.lukin.poeditor.POEditorMain'
    }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

run {
    if ( project.hasProperty('args') ) {
        args project.args.split('\\s+')
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    if(this.hasProperty('remote')) {
        sign configurations.archives
    }
}

/**
 * New way of publishing, not used for now:)
 */
publishing {
    publications {
        maven(MavenPublication) {
            groupId group
            artifactId archivesBaseName
            version version

            from components.java

            pom.withXml {
                // http://forums.gradle.org/gradle/topics/maven_publishing_plugin_doesnt_upload_generated_pom_file
                def root = asNode()
                
                root.appendNode('name', _name);
                root.appendNode('description', _description)
                root.appendNode('url', 'https://github.com/lukin0110/poeditor-java/')
                root.appendNode('inceptionYear', '2015')
                
                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/lukin0110/poeditor-java/')
                scm.appendNode('connection', 'scm:git:https://github.com/lukin0110/poeditor-java.git')
                scm.appendNode('developerConnection', 'scm:git:https://github.com/lukin0110/poeditor-java.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                //license.appendNode('distribution', 'repo')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('id', 'lukin0110')
                developer.appendNode('name', 'Maarten Huijsmans')
                developer.appendNode('email', 'maarten@lukin.be')
                
                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'GitHub issues')
                issues.appendNode('url', 'https://github.com/lukin0110/poeditor-java/issues')
            }
        }
    }
}


/**
 * Defining the repo to publish to
 */
publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url "$buildDir/repo"
        }
    }
}

/**
 * Old way of publishing, but still needed
 */
// Username & password for Sonatype, stored in gradle.properties
def _ossrhUsername = this.properties['ossrhUsername']
def _ossrhPassword = this.properties['ossrhPassword']

uploadArchives {
    repositories {
        mavenDeployer {
            if(this.hasProperty('remote')){
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: _ossrhUsername, password: _ossrhPassword)
                }
            } else {
                repository(url: uri('./build/repo'))
            }
            
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: _ossrhUsername, password: _ossrhPassword)
            }

            pom.project {
                name _name
                packaging 'jar'
                artifactId archivesBaseName
                description _description
                url 'https://github.com/lukin0110/poeditor-java/'

                scm {
                    url 'https://github.com/lukin0110/poeditor-java/'
                    connection 'scm:https://github.com/lukin0110/poeditor-java.git'
                    developerConnection 'scm:git://github.com/lukin0110/poeditor-java.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'lukin0110'
                        name 'Maarten Huijsmans'
                        email 'maarten@lukin.be'
                    }
                }

                issueManagement {
                    system 'GitHub issues'
                    url 'https://github.com/lukin0110/poeditor-java/issues'
                }
            }
        }
    }
}
